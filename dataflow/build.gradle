import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

dependencies {
    extJar module("org.springframework.cloud:spring-cloud-dataflow-server-local") {
        transitive = false
    }
}

mainClassName = "org.springframework.cloud.dataflow.server.local.LocalDataFlowServer"

task run(type: JavaExec, overwrite: true) {
    dependsOn build
    workingDir "$buildDir/libs"
    classpath files("$buildDir/libs/spring-cloud-dataflow-server-local-${rootProject.springCloudDataflowVersion}.jar")
    jvmArgs "-Dloader.path=monitoring"
    jvmArgs "-Dloader.config.location=application.yml"
    jvmArgs "-Dspring.cloud.config.enabled=false"
    main = 'org.springframework.boot.loader.PropertiesLauncher'
}

task buildImage(type: DockerBuildImage) {
    dependsOn build
    dockerFile file("$buildDir/docker/Dockerfile")
    inputDir = file("$buildDir/docker")
    tag = 'vg/dataflow:latest'
    buildArgs = [
            'VG_VERSION': version,
    ]
    doFirst {
        copy {
            from 'src/main/docker'
            from "$project.buildDir/libs"
            into "$buildDir/docker"
        }
    }
}

/*
TODO не удается сделать такую gradle-команду для в shell контейнера. Ругается на -t в параметрах
    docker run -it -e "JAVA_OPTS=-Dspring.cloud.config.enabled=false" -p=9393:9393 --rm gurv/vg-dataflow:latest /bin/ash
*/