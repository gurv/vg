plugins {
    id 'org.springframework.boot' version '2.1.8.RELEASE' apply false
    id "io.spring.dependency-management" version "1.0.8.RELEASE" apply false
    id 'com.bmuschko.docker-remote-api' version '5.2.0' apply false
    id 'com.gorylenko.gradle-git-properties' version '1.5.1' apply false
    id "org.sonarqube" version "2.7.1"
}

apply plugin: 'idea'

group = 'io.github.gurv'
version = '0.2.0-SNAPSHOT'

import com.bmuschko.gradle.docker.DockerRegistryCredentials
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.DockerTagImage

ext {
    bomVersion = '0.2.0-SNAPSHOT'
    
    email = 'vladimir.gurinovich@gmail.com'
    
    dockerhubRegistry = 'docker.io/gurv'
    githubRegistry = 'docker.pkg.github.com/gurv/vg-core'

    bootProjects = [
            project(':swagger'),
            project(':config'),
            project(':gateway'),
            project(':auth'),
            project(':account'),
            project(':metadata'),
            project(':skipper'),
            project(':skipper-shell'),
            project(':dataflow'),
            project(':dataflow-shell'),
    ]

    bootReadyProjects = [
            project(':skipper'),
            project(':skipper-shell'),
            project(':dataflow'),
            project(':dataflow-shell'),
    ]

    // monitoring & spring boot 1.5
    monitoringOldBootProjects = [
            project(':dataflow'),
            project(':skipper'),
    ]
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "gurv-github"
        property "sonar.projectVersion", "${project.version}"
        property "sonar.links.ci", "https://travis-ci.org/gurv/vg-core"
        property "sonar.links.scm", "https://github.com/gurv/vg-core"
        property "sonar.links.issue", "https://github.com/gurv/vg-core/issues"
    }
}

subprojects {
    apply plugin: 'base'
    apply plugin: 'com.bmuschko.docker-remote-api'

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/milestone" }
        jcenter()
        mavenLocal()
        maven { url "https://oss.sonatype.org/service/local/repositories/snapshots/content/" }
    }

    ext {
        imageName = "vg-${project.name}"
        imageVersion = "latest"
    }

    docker {
        registryCredentials {
            email = rootProject.email
        }
    }
}

configure(project.bootProjects) {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    //FIXME сборка swagger-aggregate с ошибкой java.io.IOException: Not a ref: refs/remotes/origin/master
    /*
        gradle api-swagger-aggregate:build

        возможно из за groovy в проекте api-swagger-aggregate
     */
    //apply plugin: 'com.gorylenko.gradle-git-properties'
    apply plugin: 'org.sonarqube'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'

    sourceCompatibility = 11
    targetCompatibility = 1.11

    bootJar {
        version = rootProject.version
        excludeDevtools = true
    }

    bootRun {
        systemProperty 'spring.cloud.stream.kafka.binder.brokers', 'localhost:9092'
    }

    springBoot {
        buildInfo()
    }

    dependencyManagement {
        imports {
            mavenBom "io.github.gurv:vg-dependencies:${rootProject.bomVersion}"
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-devtools'
        implementation 'org.codehaus.groovy:groovy-all'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'io.micrometer:micrometer-registry-prometheus'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    tasks.withType(Test) {
        systemProperties = [
                'spring.cloud.config.enabled': false,
        ]
    }

    sonarqube {
        properties {
            property "sonar.sources", "${project.projectDir}/src"
            property "sonar.java.binaries", "${project.projectDir}/build/classes"
            property "sonar.java.libraries", "${project.projectDir}/build/libs/*.jar"
            property "sonar.exclusions", "${project.projectDir}/**/*Test*/**"
            property "sonar.test.inclusions", "${project.projectDir}/**/*Test*/**"
        }
    }

    rootProject.tasks.sonarqube.dependsOn build

    //FIXME не работает gradle publish
    //    Invalid publication 'maven': artifact file does not exist: '/opt/prj/vg-core/account/build/libs/account.jar'
    publishing {
        publications {
            bootJava(MavenPublication) {
                artifact bootJar
                groupId = rootProject.group
                artifactId = "${project.name}"
                version = rootProject.version
                pom {
                    name = project.name
                    url = 'https://gurv.github.io/vg-doc/index.html'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'hhttp://www.opensource.org/licenses/mit-license.php'
                        }
                    }
                    developers {
                        developer {
                            id = 'gurv'
                            name = 'Vladimir Gurinovich'
                            email = rootProject.email
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/gurv/vg-core.git'
                        developerConnection = 'scm:git:ssh://github.com/gurv/vg-core.git'
                    }
                }
            }
            maven(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                name = 'ci'
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                url = rootProject.bomVersion.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username System.env.CI_DEPLOY_USERNAME
                    password System.env.CI_DEPLOY_PASSWORD
                }
            }
        }
    }

    sonarqube {
        properties {
            property "sonar.host.url", "https://sonarcloud.io"
            property "sonar.organization", "gurv-github"
            property "sonar.projectVersion", "${project.version}"
            property "sonar.sources", "src"
            property "sonar.java.binaries", "build/classes"
            property "sonar.java.libraries", "build/libs/*.jar"
            property "sonar.exclusions", "**/*Test*/**"
            property "sonar.test.inclusions", "**/*Test*/**"
            property "sonar.links.ci", "https://travis-ci.org/gurv/vg-core"
            property "sonar.links.scm", "https://github.com/gurv/vg-core"
            property "sonar.links.issue", "https://github.com/gurv/vg-core/issues"
        }
    }

    task buildImage(type: DockerBuildImage) {
        dependsOn build
        dockerFile = file("docker/Dockerfile")
        inputDir = file(".")
        tags.add("${project.imageName}:${project.imageVersion}")
        buildArgs = [
                'VG_VERSION': version,
                'JAR_FILE': "build/libs/${project.name}-${rootProject.version}.jar",
        ]
    }

    task tagImageDockerHub(type: DockerTagImage) {
        dependsOn buildImage
        imageId = project.imageName
        repository = "${rootProject.dockerhubRegistry}/${project.imageName}"
        tag = project.imageVersion
    }

    task pushImageToDockerHub(type: DockerPushImage) {
        dependsOn tagImageDockerHub
        imageName = "${rootProject.dockerhubRegistry}/${project.imageName}"
        tag = project.imageVersion
        def credentials = new DockerRegistryCredentials(project.objects)
        credentials.url.set rootProject.dockerhubRegistry
        credentials.username.set findProperty('dockerhubUsername') ?: System.getenv('CI_DOCKER_USERNAME')
        credentials.password.set findProperty('dockerhubPassword') ?: System.getenv('CI_DOCKER_PASSWORD')
        registryCredentials = credentials
    }

    task tagImageGitHub(type: DockerTagImage) {
        dependsOn buildImage
        imageId = project.imageName
        repository = "${rootProject.githubRegistry}/${project.imageName}"
        tag = project.imageVersion
    }

    task pushImageToGitHub(type: DockerPushImage) {
        dependsOn tagImageGitHub
        imageName = "${rootProject.githubRegistry}/${project.imageName}"
        tag = project.imageVersion
        def credentials = new DockerRegistryCredentials(project.objects)
        credentials.url.set rootProject.githubRegistry
        credentials.username.set findProperty('githubUsername') ?: System.getenv('GITHUB_USERNAME')
        credentials.password.set findProperty('githubToken') ?: System.getenv('GITHUB_TOKEN')
        registryCredentials = credentials
    }
}

configure(project.bootReadyProjects) {
    apply plugin: 'application'

    configurations {
        extJar
    }

    dependencies {
        extJar "io.github.gurv:vg-dependencies:${rootProject.bomVersion}"
    }

    build.doLast {
        copy {
            from "$project.buildDir/resources/main"
            into "$project.buildDir/libs"
        }
    }

    [jar, startScripts, distZip, distTar].each { it.enabled = false }
}

configure(project.monitoringOldBootProjects) {
    configurations {
        monitoringOldBoot
    }

    dependencies {
        monitoringOldBoot "io.github.gurv:vg-dependencies:${rootProject.bomVersion}"
        monitoringOldBoot "io.micrometer:micrometer-spring-legacy"
        monitoringOldBoot "io.micrometer:micrometer-registry-prometheus"
    }

    build.doLast {
        copy {
            from configurations.monitoringOldBoot
            into "$buildDir/monitoring"
        }
    }
}