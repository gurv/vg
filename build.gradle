plugins {
    id 'org.springframework.boot' version '2.0.3.RELEASE' apply false
    id "io.spring.dependency-management" version "1.0.7.RELEASE" apply false
    id 'com.bmuschko.docker-remote-api' version '4.6.2' apply false
    id 'com.gorylenko.gradle-git-properties' version '1.5.1' apply false
    id "org.sonarqube" version "2.7" apply false
}

apply plugin: 'idea'

group = 'io.github.gurv'
version = '0.1.0-SNAPSHOT'

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

ext {
    bomVersion = '0.1.0-SNAPSHOT'

    bootProjects = [
            project(':vg-swagger'),
            project(':vg-config'),
            project(':vg-gateway'),
            project(':vg-auth'),
            project(':vg-account'),
            project(':vg-metadata'),
            project(':vg-skipper'),
            project(':vg-skipper-shell'),
            project(':vg-dataflow'),
            project(':vg-dataflow-shell'),
    ]

    bootReadyProjects = [
            project(':vg-skipper'),
            project(':vg-skipper-shell'),
            project(':vg-dataflow'),
            project(':vg-dataflow-shell'),
    ]

    // monitoring & spring boot 1.5
    monitoringOldBootProjects = [
            project(':vg-dataflow'),
            project(':vg-skipper'),
    ]
}

subprojects {
    apply plugin: 'base'
    apply plugin: 'com.bmuschko.docker-remote-api'

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/milestone" }
        jcenter()
        mavenLocal()
        maven { url "https://oss.sonatype.org/service/local/repositories/snapshots/content/" }
    }

    ext {
        imageName = "gurv/${project.name}:latest"
    }

    docker {
        registryCredentials {
            username = System.getenv('CI_DOCKER_USERNAME')
            password = System.getenv('CI_DOCKER_PASSWORD')
            email = 'vladimir.gurinovich@gmail.com'
        }
    }
}

configure(project.bootProjects) {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    //FIXME сборка swagger-aggregate с ошибкой java.io.IOException: Not a ref: refs/remotes/origin/master
    /*
        gradle api-swagger-aggregate:build

        возможно из за groovy в проекте api-swagger-aggregate
     */
    //apply plugin: 'com.gorylenko.gradle-git-properties'
    apply plugin: 'org.sonarqube'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'

    sourceCompatibility = '1.8'

    bootJar {
        version = rootProject.version
        excludeDevtools = true
    }

    bootRun {
        systemProperty 'spring.cloud.stream.kafka.binder.brokers', 'localhost:9092'
    }

    springBoot {
        buildInfo()
    }

    dependencyManagement {
        imports {
            mavenBom "io.github.gurv:vg-dependencies:${rootProject.bomVersion}"
        }
    }

    dependencies {
        implementation "org.springframework.boot:spring-boot-devtools"
        implementation "org.codehaus.groovy:groovy-all"
        implementation "org.springframework.boot:spring-boot-starter-actuator"
        implementation 'io.micrometer:micrometer-registry-prometheus'

        testImplementation "org.springframework.boot:spring-boot-starter-test"
    }

    tasks.withType(Test) {
        systemProperties = [
                'spring.cloud.config.enabled': false,
        ]
    }

    //FIXME не работает gradle publish
    //    Invalid publication 'maven': artifact file does not exist: '/opt/prj/vg-core/account/build/libs/account.jar'
    publishing {
        publications {
            bootJava(MavenPublication) {
                artifact bootJar
                groupId = rootProject.group
                artifactId = "${project.name}"
                version = rootProject.version
                pom {
                    name = project.name
                    url = 'https://gurv.github.io/vg-doc/index.html'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'hhttp://www.opensource.org/licenses/mit-license.php'
                        }
                    }
                    developers {
                        developer {
                            id = 'gurv'
                            name = 'Vladimir Gurinovich'
                            email = 'vladimir.gurinovich@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/gurv/vg-core.git'
                        developerConnection = 'scm:git:ssh://github.com/gurv/vg-core.git'
                    }
                }
            }
            maven(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                name = 'ci'
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                url = rootProject.bomVersion.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username System.env.CI_DEPLOY_USERNAME
                    password System.env.CI_DEPLOY_PASSWORD
                }
            }
        }
    }

    task buildImage(type: DockerBuildImage) {
        dependsOn build
        dockerFile = file("docker/Dockerfile")
        inputDir = file(".")
        tags.add(imageName)
        buildArgs = [
                'VG_VERSION': version,
                'JAR_FILE': "build/libs/${project.name}-${rootProject.version}.jar",
        ]
    }

    sonarqube {
        properties {
            property "sonar.host.url", "https://sonarcloud.io"
            property "sonar.organization", "gurv-github"
            property "sonar.projectVersion", "${project.version}"
            property "sonar.sources", "src"
            property "sonar.java.binaries", "build/classes"
            property "sonar.java.libraries", "build/libs/*.jar"
            property "sonar.exclusions", "**/*Test*/**"
            property "sonar.test.inclusions", "**/*Test*/**"
            property "sonar.links.ci", "https://travis-ci.org/gurv/vg-core"
            property "sonar.links.scm", "https://github.com/gurv/vg-core"
            property "sonar.links.issue", "https://github.com/gurv/vg-core/issues"
        }
    }

    task pushImage(type: DockerPushImage) {
        dependsOn buildImage
        imageName = project.imageName
    }
}

configure(project.bootReadyProjects) {
    apply plugin: 'application'

    configurations {
        extJar
    }

    dependencies {
        extJar "io.github.gurv:vg-dependencies:${rootProject.bomVersion}"
    }

    build.doLast {
        copy {
            from "$project.buildDir/resources/main"
            into "$project.buildDir/libs"
        }
    }

    [jar, startScripts, distZip, distTar].each { it.enabled = false }
}

configure(project.monitoringOldBootProjects) {
    configurations {
        monitoringOldBoot
    }

    dependencies {
        monitoringOldBoot "io.github.gurv:vg-dependencies:${rootProject.bomVersion}"
        monitoringOldBoot "io.micrometer:micrometer-spring-legacy"
        monitoringOldBoot "io.micrometer:micrometer-registry-prometheus"
    }

    build.doLast {
        copy {
            from configurations.monitoringOldBoot
            into "$buildDir/monitoring"
        }
    }
}